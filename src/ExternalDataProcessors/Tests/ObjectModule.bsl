#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем КонтекстЯдра; // ВнешняяОбработка
Перем Ожидаем; // ВнешняяОбработка
Перем Curl; // ВнешняяОбработка
Перем ПараметрыПрокси; // Соответствие
Перем СимволОборачиванияСтроки; // Строка

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Инициализация.
// 
// Параметры:
//  КонтекстЯдраПараметр - ВнешняяОбработка - Контекст ядра параметр
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	
	НайтиИПодключитьCURL();
	ПрочитатьПараметрыПрокси();
		
КонецПроцедуры

// Заполняет набор тестов.
// 
// Параметры:
//  НаборТестов - ВнешняяОбработка - Набор тестов
//  КонтекстЯдраПараметр - ВнешняяОбработка - Контекст ядра параметр
Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	
	#Область Get
		НаборТестов.Добавить("ТестДолжен_ПолучитьДанныеПоHTTP");  
		НаборТестов.Добавить("ТестДолжен_ПолучитьДвоичныеДанныеПоHTTP");  
		НаборТестов.Добавить("ТестДолжен_ПолучитьФайлПоHTTP");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеМетодаGET");
	#КонецОбласти

	#Область Post
		НаборТестов.Добавить("ТестДолжен_УспешноВыполнитьМетодPost");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуПараметризированнойСтрокиНаСерверМетодомPost");
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуСтруктурыНаСерверМетодомPost");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуСоответствияНаСерверМетодомPost");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуДвоичныхДанныхНаСерверМетодомPost");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуФайлаНаСерверМетодомPost");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуДвоичныхДанныхВоВременномХранилищеНаСерверМетодомPost");
		НаборТестов.Добавить("ТестДолжен_ПроверитьПриОтправкеДвоичныхДанныхПередачуЗаголовкаContentType"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеМетодаPOST");
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуСодержимогоMultipart");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНевозможностьПередачиДискретныхИMultipartДанныхОдновременно");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтправкуСообщенияMultipartТолькоПоHTTP");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтправкуСообщенияMultipartТолькоДляМетодаPOST");
		НаборТестов.Добавить("ТестДолжен_ОтправитьМногострочныйТекст");
	#КонецОбласти
	
	#Область Put
		НаборТестов.Добавить("ТестДолжен_УспешноВыполнитьМетодPut"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуФайлаНаСерверМетодомPut"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуДвоичныхДанныхНаСерверМетодомPut"); 
	#КонецОбласти

	#Область АутентификацияПоHTTP
		НаборТестов.Добавить("ТестДолжен_ПроверитьПрохождениеПроверкиПодлинностиМетодомBasicПоHTTP"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьПрохождениеПроверкиПодлинностиМетодомDigest"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеАутентификацииAWS3");
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеАутентификацииBearerToken");
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуЗаголовковАутентификацииBearerToken");
	#КонецОбласти
	
	#Область ПеренаправлениеЗапросовHTTP
		НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапроса"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьЧтоЗапросНеБудетПеренаправлен"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапросаНаДругойХостБезВыполненияАвторизации"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапросаНаДругойХостСВыполнениемАвторизации");
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеПеренаправления"); 
	#КонецОбласти

	#Область ЗаголовкиHTTP
		НаборТестов.Добавить("ТестДолжен_ПроверитьПередачуHTTPЗаголовкаНаСерверМетодомGet");  
		НаборТестов.Добавить("ТестДолжен_ПроверитьПолучениеНесколькихЗаголовковОтветаSetCookie");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаЗапросаПоИмениВнеЗависимостиОтРегистра");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаОтветаПоИмениВнеЗависимостиОтРегистра");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьКомандуПриПередачеМетодаЗапросаHEAD");
	#КонецОбласти
	
	#Область Куки
		НаборТестов.Добавить("ТестДолжен_ПроверитьОбновлениеКукиПослеВыполненияЗапроса");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьЧтоКукиНеОбновленыПослеВыполненияЗапроса");	
	#КонецОбласти
	
	#Область FTP
		НаборТестов.Добавить("ТестДолжен_ПолучитьФайлПоFTP");	
		НаборТестов.Добавить("ТестДолжен_ВыполнитьАвторизациюПоFTP");
		НаборТестов.Добавить("ТестДолжен_ПровалитьАвторизациюПоFTP");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНевозможностьПолученияНесуществующегоФайлаПоFTP");	
	#КонецОбласти

	#Область Сертификаты
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиента");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаСТипом");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификата");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСКодовымКодом");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСТипом");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзОС");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзФайла");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОС");
		НаборТестов.Добавить(
			"ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеСертификатовУдостоверяющихЦентровИзФайла");
	#КонецОбласти
	
	#Область Прокси
		НаборТестов.Добавить("ТестДолжен_ВыполнитьЗапросЧерезПроксиСАутентификациейHTTP");
		НаборТестов.Добавить("ТестДолжен_ВыполнитьЗапросЧерезПроксиСАутентификациейSOCKS5");	
		
		#Область Сертификаты
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаСТипомПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСКодовымКодомПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСТипомПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзОСПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзФайлаПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОСПрокси");
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеСертификатовУдостоверяющихЦентровИзФайлаПрокси");
		#КонецОбласти
	#КонецОбласти
		
	#Область ПользовательскиеОпции
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеДобавленнойПользовательскойОпции");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеУдаленнойПользовательскойОпции");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеОчищенныхПользовательскихОпций");
	#КонецОбласти
		
	#Область НаборыШифров
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровРазделенныеЗапятой"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровРазделенныеСимволомПереноса"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровПереданныеМассивом"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьОчисткуНаборовШифров"); 		
	#КонецОбласти
	
	#Область ТаймаутыИПовторныеПопытки
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеТаймаута"); 			
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеТаймаутаСоединения"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеКоличестваПовторныхПопыток"); 				
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеЗадержкиПовторнойПопытки"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеЗадержкиПовторнойПопытки"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеТаймаутаПовторныхПопытокЗапроса"); 		
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеТаймаутаПовторныхПопытокЗапроса"); 				
	#КонецОбласти
	
	#Область СкоростьПередачиДанных
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеОграниченияСкорости");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеПрерыванияМедленнойПередачиДанных");
	#КонецОбласти
	
	#Область Прочее
		НаборТестов.Добавить("ТестДолжен_ВыброситьИсключениеПриНеверномПутиКИсполняемомуФайлу"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьОбработкуОшибокУтилиты"); 
		НаборТестов.Добавить("ТестДолжен_ВыброситьИсключениеПриПолученииОшибкиОтУтилиты"); 
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеURL");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьОпределениеМетодаЗапросаПриВыполненииСборкиСДаннымиПоHTTP");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьОпределениеМетодаЗапросаПриВыполненииСборкиБезДанныхПоHTTP");			
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеИспользованиеТихогоРежимаПоУмолчанию");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеИспользованияТихогоРежима");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеОтображенияОшибокВТихомРежимеПоУмолчанию");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеОтображенияОшибокВТихомРежиме");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеФайлаСохраненияРезультата");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеФайлаСохраненияРезультата");
		НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеФайлаСохраненияЗаголовков");
		НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеФайлаСохраненияЗаголовков");
		НаборТестов.Добавить("ТестДолжен_ОбработатьСлужебныеСимволыКомандныхИнтерпретаторов");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьПострочныйВыводОпций");	
		НаборТестов.Добавить("ТестДолжен_ПроверитьКраткуюФормуОпций");
	#КонецОбласти
				
КонецПроцедуры

// Обработчик события перед запуском теста.
Процедура ПередЗапускомТеста() Экспорт
		
	Curl = ВнешниеОбработки.Создать("cURL");
	
	СимволОборачиванияСтроки = СимволОборачиванияСтроки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Get

Процедура ТестДолжен_ПолучитьДанныеПоHTTP() Экспорт
	
	Curl.Получить("https://httpbin.org/get");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьДвоичныеДанныеПоHTTP() Экспорт
	
	Размер = 16;
	URL = СтрШаблон("https://httpbin.org/bytes/%1", Формат(Размер, "ЧГ="));
	
	ДвоичныеДанные = Curl.ПолучитьДвоичныеДанные(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(ДвоичныеДанные.Размер()).Равно(Размер);
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьФайлПоHTTP() Экспорт
	
	Размер = 16;
	URL = СтрШаблон("https://httpbin.org/bytes/%1", Формат(Размер, "ЧГ="));
	
	ПутьКФайлу = Curl.СкачатьФайл(URL);
	Файл = Новый Файл(ПутьКФайлу);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Файл.Размер()).Равно(Размер);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеМетодаGET() Экспорт
	
	Метод = "GET";
	
	Опция = ПодготовитьОпцию("--request", Метод);

	Команда = Curl.СобратьКоманду("http://example.com/", "GET");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

#КонецОбласти

#Область Post

Процедура ТестДолжен_УспешноВыполнитьМетодPost() Экспорт
	
	Curl.Отправить("https://httpbin.org/post");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуПараметризированнойСтрокиНаСерверМетодомPost() Экспорт
	
	Ключ = "Key";
	Значение = "Value";
	
	Данные = СтрШаблон("%1=%2", Ключ, Значение);	
	Ответ = Curl.Отправить("https://httpbin.org/post", Данные).ОтветКакJson();

	Ожидаем.Что(Ответ["form"][Ключ]).Равно(Значение);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуСтруктурыНаСерверМетодомPost() Экспорт
	
	Тело = Новый Структура();
	Тело.Вставить("Brand", "Audi");
	Тело.Вставить("Speed", 250);
	Тело.Вставить("IsNew", Истина);
	Тело.Вставить("Cyrillic", "Ауди");
	Тело.Вставить("SpecChars", "p=1&p=2");

	Ответ = Curl.Отправить("https://httpbin.org/post", Тело).ОтветКакJson();
	
	ПолученныеПараметры = Ответ["form"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(ПолученныеПараметры["Brand"]).Равно("Audi");
	Ожидаем.Что(ПолученныеПараметры["Speed"]).Равно("250");
	Ожидаем.Что(ПолученныеПараметры["IsNew"]).Равно("true");
	Ожидаем.Что(ПолученныеПараметры["Cyrillic"]).Равно("Ауди");
	Ожидаем.Что(ПолученныеПараметры["SpecChars"]).Равно("p=1&p=2");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуСоответствияНаСерверМетодомPost() Экспорт
	
	Тело = Новый Соответствие();
	Тело.Вставить("Brand", "BMW");
	Тело.Вставить("Speed", 280);
	Тело.Вставить("IsNew", Ложь);
	Тело.Вставить("Cyrillic", "БМВ");
	Тело.Вставить("SpecChars", "p=3&p=4");

	Ответ = Curl.Отправить("https://httpbin.org/post", Тело).ОтветКакJson();
	
	ПолученныеПараметры = Ответ["form"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(ПолученныеПараметры["Brand"]).Равно("BMW");
	Ожидаем.Что(ПолученныеПараметры["Speed"]).Равно("280");
	Ожидаем.Что(ПолученныеПараметры["IsNew"]).Равно("false");
	Ожидаем.Что(ПолученныеПараметры["Cyrillic"]).Равно("БМВ");
	Ожидаем.Что(ПолученныеПараметры["SpecChars"]).Равно("p=3&p=4");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуДвоичныхДанныхНаСерверМетодомPost() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер); 
	
	Ответ = Curl.Отправить("https://httpbin.org/post", ДвоичныеДанные).ОтветКакJson();
				
	Заголовки = Ответ["headers"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Заголовки["Content-Length"]).Равно(РазмерБуфераСтрокой);
	Ожидаем.Что(Заголовки["Content-Type"]).Равно("application/octet-stream");
	Ожидаем.Что(Ответ["data"]).Заполнено();
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуФайлаНаСерверМетодомPost() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
  	Файл = Новый Файл(ПутьКФайлу);
	
	Попытка
		Ответ = Curl.Отправить("https://httpbin.org/post", Файл).ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УдалитьФайлы(ПутьКФайлу);
				
	Заголовки = Ответ["headers"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Заголовки["Content-Length"]).Равно(РазмерБуфераСтрокой);
	Ожидаем.Что(Заголовки["Content-Type"]).Равно("application/octet-stream");
	Ожидаем.Что(Ответ["data"]).Заполнено();
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуДвоичныхДанныхВоВременномХранилищеНаСерверМетодомPost() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
  	
  	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Ответ = Curl.Отправить("https://httpbin.org/post", Адрес).ОтветКакJson();
				
	Заголовки = Ответ["headers"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Заголовки["Content-Length"]).Равно(РазмерБуфераСтрокой);
	Ожидаем.Что(Заголовки["Content-Type"]).Равно("application/octet-stream");
	Ожидаем.Что(Ответ["data"]).Заполнено();
		
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПриОтправкеДвоичныхДанныхПередачуЗаголовкаContentType() Экспорт
		
	РазмерБуфера = 16;
	
	ИмяЗаголовка = "Content-Type";
	ЗначениеЗаголовка = "application/json";
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер); 
	
	Ответ = Curl
		.УстановитьЗаголовок(ИмяЗаголовка, ЗначениеЗаголовка)
		.Отправить("https://httpbin.org/post", ДвоичныеДанные)
		.ОтветКакJson();
				
	Заголовки = Ответ["headers"];
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Заголовки[ИмяЗаголовка]).Равно(ЗначениеЗаголовка);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеМетодаPOST() Экспорт
	
	Метод = "POST";
	
	Опция = ПодготовитьОпцию("--request", Метод);

	Команда = Curl.СобратьКоманду("http://example.com/", "POST");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуСодержимогоMultipart() Экспорт
	
	ДвоичныеДанные = ДвоичныеДанные16Байт();
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Curl
		.ДобавитьЗаписьMultipart("Name", "Tom", "text/plain")
		.ДобавитьЗаписьMultipart("Age", "45")
		.ДобавитьФайлMultipart("Booking", ДвоичныеДанные, , "receipt.html")
		.ДобавитьФайлMultipart("Tickets", ПутьКФайлу, "application/pdf")
		.ДобавитьФайлMultipart("Visa", АдресВоВременномХранилище);
		
	Попытка
		Ответ = Curl.Отправить("https://httpbin.org/post").ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
		
	УдалитьФайлы(ПутьКФайлу);
	
	ДанныеФормы = Ответ["form"];
	Файлы = Ответ["files"];
	
	Ожидаем.Что(Ответ["headers"]["Content-Type"]).Содержит("multipart/form-data");
	Ожидаем.Что(ДанныеФормы.Получить("Name")).Равно("Tom");
	Ожидаем.Что(ДанныеФормы.Получить("Age")).Равно("45");
	Ожидаем.Что(Файлы.Получить("Booking")).Не_().Равно(Неопределено);
	Ожидаем.Что(Файлы.Получить("Tickets")).Не_().Равно(Неопределено);
	Ожидаем.Что(Файлы.Получить("Visa")).Не_().Равно(Неопределено);
			
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНевозможностьПередачиДискретныхИMultipartДанныхОдновременно() Экспорт
	
	Curl.ДобавитьЗаписьMultipart("Name", "Tom");
	
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить("https://httpbin.org/post");
	ПараметрыМетода.Добавить("Name=Value");
	
	Ожидаем.Метод("СобратьКоманду", ПараметрыМетода);
	Ожидаем.Что(Curl).ВыбрасываетИсключение("Одновременная передача дискретных и multipart данных недоступна");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтправкуСообщенияMultipartТолькоПоHTTP() Экспорт
	
	Curl.ДобавитьЗаписьMultipart("Name", "Tom");
	
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить("ftp://ftp.example.com");
	
	Ожидаем.Метод("СобратьКоманду", ПараметрыМетода);
	Ожидаем.Что(Curl).ВыбрасываетИсключение("Передача сообщения multipart доступна только при работе с HTTP протоколом");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтправкуСообщенияMultipartТолькоДляМетодаPOST() Экспорт
	
	Curl.ДобавитьЗаписьMultipart("Name", "Tom");
	
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить("https://httpbin.org/get");
	ПараметрыМетода.Добавить(Неопределено);
	ПараметрыМетода.Добавить("GET");
	
	Ожидаем.Метод("СобратьКоманду", ПараметрыМетода);
	Ожидаем.Что(Curl).ВыбрасываетИсключение("Передача сообщения multipart доступна только в POST запросе");
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьМногострочныйТекст() Экспорт
	
	Ключ1 = "Key1";
	Значение1 = "Value1";
	
	Ключ2 = "Key2";
	Значение2 = "Value2";
		
	Данные = СтрШаблон("%1=%2
	|&%3=%4", Ключ1, Значение1, Ключ2, Значение2);	
	
	Ответ = Curl.Отправить("https://httpbin.org/post", Данные).ОтветКакJson();

	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["form"][Ключ1]).Равно(Значение1);
	Ожидаем.Что(Ответ["form"][Ключ2]).Равно(Значение2);
	
КонецПроцедуры

#КонецОбласти

#Область Put

Процедура ТестДолжен_УспешноВыполнитьМетодPut() Экспорт
	
	Curl.Отправить("https://httpbin.org/put", , "PUT");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуФайлаНаСерверМетодомPut() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
  	Файл = Новый Файл(ПутьКФайлу);
	
	Попытка
		Ответ = Curl.Отправить("https://httpbin.org/put", Файл, "PUT").ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УдалитьФайлы(ПутьКФайлу);
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуДвоичныхДанныхНаСерверМетодомPut() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	Ответ = Curl.Отправить("https://httpbin.org/put", ДвоичныеДанные, "PUT").ОтветКакJson();
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

#КонецОбласти

#Область АутентификацияПоHTTP

Процедура ТестДолжен_ПроверитьПрохождениеПроверкиПодлинностиМетодомBasicПоHTTP() Экспорт

	ИмяПользователя = "user";
	Пароль = "secret";
	
	URL = СтрШаблон("https://httpbin.org/basic-auth/%1/%2", ИмяПользователя, Пароль);
	Ответ = Curl.Аутентификация(ИмяПользователя, Пароль)
				.Получить(URL)
				.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["authenticated"]).Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПрохождениеПроверкиПодлинностиМетодомDigest() Экспорт
	
	ИмяПользователя = "user";
	Пароль = "secret";
	
	URL = СтрШаблон("https://httpbin.org/digest-auth/auth/%1/%2", ИмяПользователя, Пароль);
	Ответ = Curl.АутентификацияDigest("user", "secret")
				.Получить(URL)
				.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["authenticated"]).Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеАутентификацииAWS3() Экспорт
	
	КлючДоступа = "access_key";
	СекретныйКлюч = "secret_key";
	Регион = "eu-west-1";
	Сервис = "s3";
	
	Опция1 = ПодготовитьОпцию("--aws-sigv4", СтрШаблон("aws:amz:%1:%2", Регион, Сервис));
	Опция2 = ПодготовитьОпцию("--user", СтрШаблон("%1:%2", КлючДоступа, СекретныйКлюч));

	Curl.АутентификацияAWS4(КлючДоступа, СекретныйКлюч, Регион, Сервис);
	СтрокаКоманды = Curl.СобратьКоманду("https://amazonaws.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеАутентификацииBearerToken() Экспорт
	
	Токен = "token";
	
	Опция = ПодготовитьОпцию("--oauth2-bearer", Токен);

	Curl.АутентификацияBearerToken(Токен);
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com/auth");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуЗаголовковАутентификацииBearerToken() Экспорт

	Токен = "token";
	
	Ответ = Curl.АутентификацияBearerToken(Токен)
				.ПолучитьJson("https://httpbin.org/bearer");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["authenticated"]).Равно(Истина);
	Ожидаем.Что(Ответ["token"]).Равно(Токен);
	
КонецПроцедуры

#КонецОбласти

#Область ПеренаправлениеЗапросовHTTP

Процедура ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапроса() Экспорт
	
	Location = КодироватьСтроку("https://httpbin.org/status/200", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Curl.РазрешатьПеренаправления()
		.Получить(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоЗапросНеБудетПеренаправлен() Экспорт
	
	Location = КодироватьСтроку("https://httpbin.org/status/200", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Curl.Получить(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP301());
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапросаНаДругойХостБезВыполненияАвторизации() Экспорт
	
	Location = КодироватьСтроку("https://postman-echo.com/headers", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Ответ = Curl
		.РазрешатьПеренаправления()
		.Аутентификация("user", "secret")
		.Получить(URL)
		.ОтветКакJson();
		
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["headers"].Получить("authorization")).ЭтоНеопределено();
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыполнениеПеренаправленияЗапросаНаДругойХостСВыполнениемАвторизации() Экспорт

	Location = КодироватьСтроку("https://postman-echo.com/headers", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);

	Ответ = Curl
		.РазрешатьПеренаправления(Истина, Истина)
		.Аутентификация("user", "secret")
		.Получить(URL)
		.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ["headers"].Получить("authorization")).Не_().ЭтоНеопределено();
		
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеПеренаправления() Экспорт
	
	МаксимальноеКоличествоПеренаправлений = 3;

	КлючПеренаправления = "--location";
	КлючМаксКоличествоПеренаправлений = "--max-redirs " + Формат(МаксимальноеКоличествоПеренаправлений, ФорматЧисла());

	СтрокаКоманды = Curl
		.РазрешатьПеренаправления(Истина, Ложь, МаксимальноеКоличествоПеренаправлений)
		.СобратьКоманду("https://example.com");
	
	Ожидаем.Что(СтрокаКоманды).Содержит(КлючПеренаправления);
	Ожидаем.Что(СтрокаКоманды).Содержит(КлючМаксКоличествоПеренаправлений);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаголовкиHTTP

Процедура ТестДолжен_ПроверитьПередачуHTTPЗаголовкаНаСерверМетодомGet() Экспорт
	
	Агент = "1C:Enterprise";
	Ответ = Curl.УстановитьЗаголовок("User-Agent", Агент)
				.Получить("https://httpbin.org/get")
				.ОтветКакJson();
	
	Ожидаем.Что(Ответ["headers"]["User-Agent"]).Равно(Агент);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПолучениеНесколькихЗаголовковОтветаSetCookie() Экспорт
	
	Опция1 = КодироватьСтроку("name=Jack", СпособКодированияСтроки.КодировкаURL);
	Опция2 = КодироватьСтроку("age=25", СпособКодированияСтроки.КодировкаURL);
	КоличествоЗаголовков = 2;
	
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1&set-cookie=%2", Опция1, Опция2);

	ЗаголовкиОтвета = Curl
		.Получить(URL)
		.ЗаголовкиОтвета();
	
	ЗначениеЗаголовка = ЗаголовкиОтвета.Получить("set-cookie");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(ЗначениеЗаголовка, "Ожидаем тип Массив").ИмеетТип(Тип("Массив"));
	Ожидаем.Что(ЗначениеЗаголовка.Количество(), "Ожидаем 2 элемента").Равно(КоличествоЗаголовков);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаЗапросаПоИмениВнеЗависимостиОтРегистра() Экспорт
	
	Опция = "name=value";
	Curl.УстановитьЗаголовок("COOKIE", Опция);
	
	Значение = Curl.Заголовок("Cookie");
	
	Ожидаем.Что(Значение).Равно(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаОтветаПоИмениВнеЗависимостиОтРегистра() Экспорт
	
	Опция = "text/html; charset=utf-8";
	
	Параметр = КодироватьСтроку(Опция, СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?Content-Type=%1", Параметр);

	Значение = Curl
		.Получить(URL)
		.ЗаголовокОтвета("Content-TYPE");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Значение).Равно(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьКомандуПриПередачеМетодаЗапросаHEAD() Экспорт
	
	Команда = Curl.СобратьКоманду("https://httpbin.org/get",, "HEAD");
	
	Ожидаем.Что(Команда).Содержит("--head");	
	Ожидаем.Что(Команда).Не_().Содержит("--request");	
		
КонецПроцедуры

#КонецОбласти

#Область Куки

Процедура ТестДолжен_ПроверитьОбновлениеКукиПослеВыполненияЗапроса() Экспорт
	
	Параметр = КодироватьСтроку("Age=18", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1", Параметр);

	Куки = Curl
		.УстановитьКуки("Name=Jhon")
		.Получить(URL)
		.Куки();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Куки.Получить("Name")).Равно("Jhon");
	Ожидаем.Что(Куки.Получить("Age")).Равно("18");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоКукиНеОбновленыПослеВыполненияЗапроса() Экспорт
	
	Параметр = КодироватьСтроку("Age=18", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1", Параметр);

	Куки = Curl
		.УстановитьКуки("Name=Jhon")
		.ОбновлятьКуки(Ложь)
		.Получить(URL)
		.Куки();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Куки.Получить("Name")).Равно("Jhon");
	Ожидаем.Что(Куки.Получить("Age")).ЭтоНеопределено();
	
КонецПроцедуры

#КонецОбласти

#Область FTP

Процедура ТестДолжен_ПолучитьФайлПоFTP() Экспорт
	
	Текст = Curl.ПолучитьТекст("ftp://demo:password@test.rebex.net/readme.txt");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияFTP226());
	Ожидаем.Что(Текст).Заполнено();
	
КонецПроцедуры

Процедура ТестДолжен_ВыполнитьАвторизациюПоFTP() Экспорт
	
	Curl.Аутентификация("demo", "password")
		.Получить("ftp://test.rebex.net");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияFTP226());
	
КонецПроцедуры

Процедура ТестДолжен_ПровалитьАвторизациюПоFTP() Экспорт
	
	Curl.Аутентификация("demo", "incorrect_password")
		.Получить("ftp://test.rebex.net");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияFTP530());
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНевозможностьПолученияНесуществующегоФайлаПоFTP() Экспорт
	
	Curl.Получить("ftp://demo:password@test.rebex.net/non-existent_file.txt");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияFTP550());
	
КонецПроцедуры

#КонецОбласти

#Область Сертификаты

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиента() Экспорт
	
	ИмяФайлаСертификата = "certfile";   
	Пароль = "password";  
	
	Опция = ПодготовитьОпцию("--cert", СтрШаблон("%1:%2", ИмяФайлаСертификата, Пароль));

	Curl.УстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаСТипом() Экспорт
	
	ИмяФайлаСертификата = "certfile";
	Пароль = "password";
	ТипСертификата = "PEM";

	Опция1 = ПодготовитьОпцию("--cert", СтрШаблон("%1:%2", ИмяФайлаСертификата, Пароль));
	Опция2 = ПодготовитьОпцию("--cert-type", ТипСертификата);

	Curl.УстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль, ТипСертификата);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификата() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	
	Опция = ПодготовитьОпцию("--key", ИмяФайлаЗакрытогоКлюча);

	Curl.УстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСКодовымКодом() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";
	КодовоеСлово = "secret";
	
	Опция1 = ПодготовитьОпцию("--key", ИмяФайлаЗакрытогоКлюча);
	Опция2 = ПодготовитьОпцию("--pass", КодовоеСлово);

	Curl.УстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча, КодовоеСлово);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
		
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСТипом() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	ТипЗакрытогоКлюча = "PEM";
	
	Опция1 = ПодготовитьОпцию("--key", ИмяФайлаЗакрытогоКлюча);
	Опция2 = ПодготовитьОпцию("--key-type", ТипЗакрытогоКлюча);

	Curl.УстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча, , ТипЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзОС() Экспорт
	
	Опция = "--ca-native";
	
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзФайла() Экспорт
	
	ИмяФайла = "CA-file.txt";

	Опция = ПодготовитьОпцию("--cacert", ИмяФайла);
	
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОС() Экспорт
	
	ИмяФайла = "CA-file.txt";
	КлючСертификатыУЦИзОС = "--ca-native";
	
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(КлючСертификатыУЦИзОС);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеСертификатовУдостоверяющихЦентровИзФайла() Экспорт
	
	ИмяФайла = "CA-file.txt";
	КлючСертификатыУЦИзФайла = "--cacert";
	
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(КлючСертификатыУЦИзФайла);
	
КонецПроцедуры

#КонецОбласти

#Область Прокси

Процедура ТестДолжен_ВыполнитьЗапросЧерезПроксиСАутентификациейHTTP() Экспорт
	
	Параметры = ПараметрыПрокси["http"];
	
	Curl.УстановитьПрокси("http", Параметры["host"], Параметры["port"])
		.АутентификацияПрокси(Параметры["user"], Параметры["pass"])
		.Получить("https://httpbin.org/get");
		
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

Процедура ТестДолжен_ВыполнитьЗапросЧерезПроксиСАутентификациейSOCKS5() Экспорт
	
	Параметры = ПараметрыПрокси["socks5"];
	
	Curl.УстановитьПрокси("socks5", Параметры["host"], Параметры["port"])
		.АутентификацияПрокси(Параметры["user"], Параметры["pass"])
		.РазрешатьПеренаправления()
		.Получить("https://www.ya.ru/");
		
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	
КонецПроцедуры

#Область Сертификаты

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаПрокси() Экспорт
	
	ИмяФайлаСертификата = "certfile";   
	Пароль = "password";  
	
	Опция = ПодготовитьОпцию("--proxy-cert", СтрШаблон("%1:%2", ИмяФайлаСертификата, Пароль));

	Curl.ПроксиУстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатКлиентаСТипомПрокси() Экспорт
	
	ИмяФайлаСертификата = "certfile";
	Пароль = "password";
	ТипСертификата = "PEM";
	
	Опция1 = ПодготовитьОпцию("--proxy-cert", СтрШаблон("%1:%2", ИмяФайлаСертификата, Пароль));
	Опция2 = ПодготовитьОпцию("--proxy-cert-type", ТипСертификата);

	Curl.ПроксиУстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль, ТипСертификата);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаПрокси() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	
	Опция = ПодготовитьОпцию("--proxy-key", ИмяФайлаЗакрытогоКлюча);

	Curl.ПроксиУстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСКодовымКодомПрокси() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";
	КодовоеСлово = "secret";
	
	Опция1 = ПодготовитьОпцию("--proxy-key", ИмяФайлаЗакрытогоКлюча);
	Опция2 = ПодготовитьОпцию("--proxy-pass", КодовоеСлово);

	Curl.ПроксиУстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча, КодовоеСлово);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
		
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеЗакрытогоКлючаСертификатаСТипомПрокси() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	ТипЗакрытогоКлюча = "PEM";
	
	Опция1 = ПодготовитьОпцию("--proxy-key", ИмяФайлаЗакрытогоКлюча);
	Опция2 = ПодготовитьОпцию("--proxy-key-type", ТипЗакрытогоКлюча);

	Curl.ПроксиУстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча, , ТипЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзОСПрокси() Экспорт
	
	Опция = "--proxy-ca-native";
	
	Curl.ПроксиИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыИспользованиеСертификатовУдостоверяющихЦентровИзФайлаПрокси() Экспорт
	
	ИмяФайла = "CA-file.txt";

	Опция = ПодготовитьОпцию("--proxy-cacert", ИмяФайла);
	
	Curl.ПроксиУстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОСПрокси() Экспорт
	
	ИмяФайла = "CA-file.txt";
	КлючСертификатыУЦИзОС = "--proxy-ca-native";
	
	Curl.ПроксиИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	Curl.ПроксиУстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(КлючСертификатыУЦИзОС);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыОтсутствиеСертификатовУдостоверяющихЦентровИзФайлаПрокси() Экспорт
	
	ИмяФайла = "CA-file.txt";
	КлючСертификатыУЦИзФайла = "--proxy-cacert";
	
	Curl.ПроксиУстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	Curl.ПроксиИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(КлючСертификатыУЦИзФайла);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПользовательскиеОпции

Процедура ТестДолжен_ПроверитьНаличиеВКомандеДобавленнойПользовательскойОпции() Экспорт
	
	Curl.ДобавитьОпцию("--limit-rate", "1000");
	
	Опция = ПодготовитьОпцию("--limit-rate", "1000");
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеУдаленнойПользовательскойОпции() Экспорт
	
	Опция = "--limit-rate";
	
	Curl.ДобавитьОпцию(Опция, "1000");
	Curl.УдалитьОпцию(Опция);
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеОчищенныхПользовательскихОпций() Экспорт
	
	Curl.ДобавитьОпцию("--limit-rate", "1000");
	Curl.ОчиститьОпции();
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит("--limit-rate");
	
КонецПроцедуры

#КонецОбласти

#Область НаборыШифров

Процедура ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровРазделенныеЗапятой() Экспорт
	
	НаборШифров = НаборыШифровПоУмолчанию();
	
	НаборыШифровСтрокой = СтрСоединить(НаборШифров, ",");
	
	Опция = ПодготовитьОпцию("--ciphers", НаборыШифровСтрокой);

	Curl.ДобавитьНаборыШифров(НаборыШифровСтрокой);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровРазделенныеСимволомПереноса() Экспорт
	
	НаборШифров = НаборыШифровПоУмолчанию();
		
	НаборыШифровСтрокой = СтрСоединить(НаборШифров, ",");
	Опция = ПодготовитьОпцию("--ciphers", НаборыШифровСтрокой);

	НаборыШифровСтрокой = СтрСоединить(НаборШифров, Символы.ПС);
	Curl.ДобавитьНаборыШифров(НаборыШифровСтрокой);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВСтрокеКомандыНаборыШифровПереданныеМассивом() Экспорт
	
	НаборШифров = НаборыШифровПоУмолчанию();
		
	НаборыШифровСтрокой = СтрСоединить(НаборШифров, ",");
	Опция = ПодготовитьОпцию("--ciphers", НаборыШифровСтрокой);

	Curl.ДобавитьНаборыШифров(НаборШифров);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОчисткуНаборовШифров() Экспорт
	
	НаборШифров = НаборыШифровПоУмолчанию();

	Curl.ДобавитьНаборыШифров(НаборШифров);
	Curl.ОчиститьНаборыШифров();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит("--ciphers");
	
КонецПроцедуры

#КонецОбласти

#Область ТаймаутыИПовторныеПопытки

Процедура ТестДолжен_ПроверитьНаличиеВКомандеТаймаута() Экспорт
	
	Таймаут = 30;
	Опция = "--max-time " + Формат(Таймаут, ФорматЧисла());
	
	Curl.УстановитьТаймаут(Таймаут);
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеТаймаутаСоединения() Экспорт
	
	ТаймаутСоединения = 30;
	Опция = "--connect-timeout " + Формат(ТаймаутСоединения, ФорматЧисла());
	
	Curl.УстановитьТаймаутСоединения(ТаймаутСоединения);
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеКоличестваПовторныхПопыток() Экспорт
	
	КоличествоПовторныхПопыток = 10;
	Опция = "--retry " + Формат(КоличествоПовторныхПопыток, ФорматЧисла());
	
	Команда = Curl
		.УстановитьКоличествоПопыток(КоличествоПовторныхПопыток)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеЗадержкиПовторнойПопытки() Экспорт
	
	КоличествоПовторныхПопыток = 10;
	ЗадержкаПовторнойПопытки = 10;
	Опция = "--retry-delay " + Формат(ЗадержкаПовторнойПопытки, ФорматЧисла());
	
	Команда = Curl
		.УстановитьКоличествоПопыток(КоличествоПовторныхПопыток)
		.УстановитьЗадержкуПовторнойПопытки(ЗадержкаПовторнойПопытки)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеЗадержкиПовторнойПопытки() Экспорт
	
	ЗадержкаПовторнойПопытки = 10;
	Опция = "--retry-delay " + Формат(ЗадержкаПовторнойПопытки, ФорматЧисла());
	
	Команда = Curl
		.УстановитьЗадержкуПовторнойПопытки(ЗадержкаПовторнойПопытки)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеТаймаутаПовторныхПопытокЗапроса() Экспорт
	
	КоличествоПовторныхПопыток = 10;
	ТаймаутПовторнойПопыткиЗапроса = 30;
	Опция = "--retry-max-time " + Формат(ТаймаутПовторнойПопыткиЗапроса, ФорматЧисла());
	
	Команда = Curl
		.УстановитьКоличествоПопыток(КоличествоПовторныхПопыток)
		.УстановитьТаймаутПовторныхПопыток(ТаймаутПовторнойПопыткиЗапроса)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеТаймаутаПовторныхПопытокЗапроса() Экспорт
	
	ТаймаутПовторныхПопыток = 30;
	Опция = "--retry-max-time " + Формат(ТаймаутПовторныхПопыток, ФорматЧисла());
	
	Команда = Curl
		.УстановитьТаймаутПовторныхПопыток(ТаймаутПовторныхПопыток)
		.СобратьКоманду("http://example.com/");

	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

#КонецОбласти

#Область СкоростьПередачиДанных

Процедура ТестДолжен_ПроверитьНаличиеВКомандеОграниченияСкорости() Экспорт
	
	МаксимальнаяСкорость = "100M";
	
	Опция = ПодготовитьОпцию("--limit-rate", МаксимальнаяСкорость);
	
	Команда = Curl
		.ОграничитьСкорость(МаксимальнаяСкорость)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеПрерыванияМедленнойПередачиДанных() Экспорт
	
	МинимальнаяСкорость = 1024;
	Секунд = 60;
	
	ОпцияСкорости = "--speed-limit " + Формат(МинимальнаяСкорость, ФорматЧисла());
	ОпцияВреми = "--speed-time " + Формат(Секунд, ФорматЧисла());
	
	Команда = Curl
		.ПрерыватьМедленнуюПередачуДанных(МинимальнаяСкорость, Секунд)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(ОпцияСкорости);
	Ожидаем.Что(Команда).Содержит(ОпцияВреми);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ТестДолжен_ВыброситьИсключениеПриНеверномПутиКИсполняемомуФайлу() Экспорт
	
	БылоИсключение = Ложь;
	Попытка
		Curl.УказатьИсполняемыйФайл("ccurl")
			.Получить("https://httpbin.org/get");
	Исключение
		БылоИсключение = Истина;
	КонецПопытки;
	
	Ожидаем.Что(БылоИсключение, "Не выбросилось исключение при неверном пути к исполняемому файлу").Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОбработкуОшибокУтилиты() Экспорт
	
	КодВозврата = 6; // Could not resolve host. The given remote host could not be resolved.
	
	Curl.Получить("http://non-existent.url");
	
	Ожидаем.Что(Curl.КодВозврата()).Равно(КодВозврата);
	Ожидаем.Что(Curl.ТекстОшибки()).Заполнено();
	
КонецПроцедуры

Процедура ТестДолжен_ВыброситьИсключениеПриПолученииОшибкиОтУтилиты() Экспорт

	КодВозврата = 6; // Could not resolve host. The given remote host could not be resolved.
	
	БылоИсключение = Ложь;
	Попытка	
		Curl.ВыбрасыватьИсключение()
			.Получить("http://non-existent.url");
	Исключение
		БылоИсключение = Истина;
	КонецПопытки;
	
	Ожидаем.Что(БылоИсключение).Равно(Истина);
	Ожидаем.Что(Curl.КодВозврата()).Равно(КодВозврата);
	Ожидаем.Что(Curl.ТекстОшибки()).Заполнено();
			
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеURL() Экспорт
	
	URL = "http://example.com/";
	
	Команда = Curl.СобратьКоманду(URL);
	
	Ожидаем.Что(Команда).Содержит(ОбернутьКавычками(URL));
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОпределениеМетодаЗапросаПриВыполненииСборкиСДаннымиПоHTTP() Экспорт
	
	Опция = ПодготовитьОпцию("--request", "POST");

	Команда = Curl.СобратьКоманду("http://example.com/", "Key=Value");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОпределениеМетодаЗапросаПриВыполненииСборкиБезДанныхПоHTTP() Экспорт
	
	Опция = "--request";

	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеИспользованиеТихогоРежимаПоУмолчанию() Экспорт
	
	Опция = "--silent";

	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеИспользованияТихогоРежима() Экспорт
	
	Опция = "--silent";

	Команда = Curl
		.ИспользоватьТихийРежим(Ложь)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеОтображенияОшибокВТихомРежимеПоУмолчанию() Экспорт
	
	Опция = "--show-error";

	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеОтображенияОшибокВТихомРежиме() Экспорт
	
	Опция = "--show-error";

	Команда = Curl
		.ПоказыватьОшибкиВТихомРежиме(Ложь)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеФайлаСохраненияРезультата() Экспорт
	
	ПутьКФайлу = "path/to/output";
	Опция = ПодготовитьОпцию("--output", ПутьКФайлу);

	Команда = Curl
		.УказатьФайлСохраненияРезультата(ПутьКФайлу)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеФайлаСохраненияРезультата() Экспорт
	
	Опция = "--output";

	Команда = Curl
		.УказатьФайлСохраненияРезультата("")
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеФайлаСохраненияЗаголовков() Экспорт
	
	ПутьКФайлу = "path/to/headers";
	Опция = ПодготовитьОпцию("--dump-header", ПутьКФайлу);

	Команда = Curl
		.УказатьФайлСохраненияЗаголовков(ПутьКФайлу)
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеФайлаСохраненияЗаголовков() Экспорт
	
	Опция = "--dump-header";

	Команда = Curl
		.УказатьФайлСохраненияЗаголовков("")
		.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ОбработатьСлужебныеСимволыКомандныхИнтерпретаторов() Экспорт
	
	МассивСимволов = Новый Массив;
	МассивСимволов.Добавить("\"); 
	МассивСимволов.Добавить("\\");
	МассивСимволов.Добавить("!");
	МассивСимволов.Добавить("'");	
	МассивСимволов.Добавить("'\");	
	МассивСимволов.Добавить("'!");
	МассивСимволов.Добавить("""");
	МассивСимволов.Добавить("%");
	МассивСимволов.Добавить("@");
	МассивСимволов.Добавить("<");
	МассивСимволов.Добавить(">");
	МассивСимволов.Добавить(">>");	
	МассивСимволов.Добавить("<<");
	МассивСимволов.Добавить("|");
	МассивСимволов.Добавить("||");
	МассивСимволов.Добавить("&");
	МассивСимволов.Добавить("&&");
	МассивСимволов.Добавить("&>");
	МассивСимволов.Добавить(">&");
	МассивСимволов.Добавить("<&");
	МассивСимволов.Добавить("$");
	МассивСимволов.Добавить("$!");
	МассивСимволов.Добавить("\$");
	МассивСимволов.Добавить("^");
	
	Инд = 0;
	Для Каждого Символ Из МассивСимволов Цикл
		Инд = Инд + 1;
		Curl.УстановитьЗаголовок("Test" + Инд, Символ);	
	КонецЦикла;

	Ответ = Curl.ПолучитьJson("https://httpbin.org/get");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ).Заполнено();
	Ожидаем.Что(Ответ.Получить("headers")).Заполнено();
	
	Инд = 0;
	Для Каждого Символ Из МассивСимволов Цикл
		Инд = Инд + 1;
		Имя = "Test" + Инд;
		Ожидаем.Что(Ответ["headers"].Получить(Имя)).Заполнено();
		Ожидаем.Что(Ответ["headers"][Имя]).Равно(Символ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьПострочныйВыводОпций() Экспорт
	
	КоличествоСтрок = 5;
	
	Curl
		.УстановитьПострочныйВыводОпций()
		.УстановитьЗаголовок("Test1", "Value 1")
		.УстановитьЗаголовок("Test2", "Value 2");
		
	Команда = Curl.СобратьКоманду("https://httpbin.org/get");
	Ответ = Curl.ПолучитьJson("https://httpbin.org/get");
	
	Ожидаем.Что(СтрЧислоСтрок(Команда)).Равно(КоличествоСтрок);
	Ожидаем.Что(Curl.КодСостояния()).Равно(КодСостоянияHTTP200());
	Ожидаем.Что(Ответ).Заполнено();
	
	Заголовки = Ответ.Получить("headers");
	
	Ожидаем.Что(Заголовки).Заполнено();	
	Ожидаем.Что(Заголовки.Получить("Test1")).Равно("Value 1");
	Ожидаем.Что(Заголовки.Получить("Test2")).Равно("Value 2");
		
КонецПроцедуры

Процедура ТестДолжен_ПроверитьКраткуюФормуОпций() Экспорт
	
	Ответ = Curl
		.ИспользоватьКраткуюФормуОпций()
		.УстановитьЗаголовок("Test1", "Value 1")
		.УстановитьЗаголовок("Test2", "Value 2")
		.ПолучитьJson("https://httpbin.org/get");
	
	Заголовки = Ответ.Получить("headers");
	
	Ожидаем.Что(Заголовки).Заполнено();	
	Ожидаем.Что(Заголовки.Получить("Test1")).Равно("Value 1");
	Ожидаем.Что(Заголовки.Получить("Test2")).Равно("Value 2");
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиИПодключитьCURL()
	
	ПолноеИмя = НайтиФайлВТекущемКаталоге("cURL.epf");
	Если ПолноеИмя = Неопределено Тогда
		ВызватьИсключение "Не найден файл cURL.epf в каталоге с тестами";
	КонецЕсли;
	
	ВнешниеОбработки.Создать(ПолноеИмя, Ложь);
	
КонецПроцедуры

// Прочитать параметры прокси.
//
// Пример содержимого файла:
//	{
//		"http": {
//			"host": "127.0.0.1",
//			"port": "1080",
//			"user": "user",
//			"pass": "secret"
//		},
//		"socks5": {
//			"host": "127.0.0.1",
//			"port": "1080",
//			"user": "user",
//			"pass": "secret"
//		}
//	}
// 
// 
Процедура ПрочитатьПараметрыПрокси()
	
	ПолноеИмя = НайтиФайлВТекущемКаталоге("proxy.json");
	Если ПолноеИмя = Неопределено Тогда
		ВызватьИсключение "Не найден файл proxy.json в каталоге с тестами";
	КонецЕсли;
	
	ПараметрыПрокси = ПрочитатьФайлJSON(ПолноеИмя);
	
КонецПроцедуры

Функция НайтиФайлВТекущемКаталоге(Имя)
	Файлы = НайтиФайлы(ПутьКТекущемуКаталогу(), Имя);
	Если Файлы.Количество() Тогда
		Возврат Файлы[0].ПолноеИмя;
	КонецЕсли;	
КонецФункции

Функция ПутьКТекущемуКаталогу()
	Файл = Новый Файл(ИспользуемоеИмяФайла);
	Возврат Файл.Путь;
КонецФункции

Функция ПрочитатьФайлJSON(ПутьКФайлу)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ПутьКФайлу);
	
	Результат = ПрочитатьJSON(ЧтениеJSON, Истина);

	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция НаборыШифровПоУмолчанию()
	НаборШифров = Новый Массив;
	НаборШифров.Добавить("TLS_AES_128_GCM_SHA256");
	НаборШифров.Добавить("TLS_AES_256_GCM_SHA384");	
	Возврат НаборШифров;
КонецФункции

Функция ФорматЧисла()
	Возврат "ЧРД=.; ЧГ=;";
КонецФункции

Функция ДвоичныеДанные16Байт()

	РазмерБуфера = 16;

	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	УдалитьФайлы(ПутьКФайлу);
	
	Возврат ДвоичныеДанные;
	
КонецФункции

Функция ПодготовитьОпцию(Ключ, Значение, ОборачиватьКавычками = Истина)
	Возврат СтрШаблон("%1 %2", Ключ, ?(ОборачиватьКавычками, ОбернутьКавычками(Значение), Значение));	
КонецФункции

// Оборачивает строку кавычками.
// 
// Параметры:
//  Строка - Строка - Строка
// 
// Возвращаемое значение:
//  Строка
Функция ОбернутьКавычками(Строка)
	Возврат СтрШаблон("%1%2%1", СимволОборачиванияСтроки, Строка);
КонецФункции

Функция СимволОборачиванияСтроки()
	Если ПолучитьКомандныйИнтерпретатор() = ИмяКомандногоИнтерпретатораBash() Тогда
		Возврат СимволОборачиванияСтрокиBash();
	ИначеЕсли ПолучитьКомандныйИнтерпретатор() = ИмяКомандногоИнтерпретатораCmd() Тогда
		Возврат СимволОборачиванияСтрокиCmd();
	Иначе
		ВызватьИсключение "Не удалось определить символ оборачивания строки";
	КонецЕсли;	
КонецФункции

Функция СимволОборачиванияСтрокиCmd()
	Возврат """";
КонецФункции

Функция СимволОборачиванияСтрокиBash()
	Возврат "'";
КонецФункции

Функция ПолучитьКомандныйИнтерпретатор()
	Если ЭтоWindows() Тогда
		Возврат ИмяКомандногоИнтерпретатораCmd();
	Иначе	
		Возврат ИмяКомандногоИнтерпретатораBash();
	КонецЕсли;
КонецФункции

Функция ИмяКомандногоИнтерпретатораCmd()
	Возврат "cmd";	
КонецФункции

Функция ИмяКомандногоИнтерпретатораBash()
	Возврат "bash";	
КонецФункции

Функция ЭтоWindows()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 
		Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64;
КонецФункции

#Область КодыСостояний

Функция КодСостоянияHTTP200()
	Возврат 200;
КонецФункции

Функция КодСостоянияHTTP301()
	Возврат 301;
КонецФункции

Функция КодСостоянияFTP226()
	Возврат 226;
КонецФункции

Функция КодСостоянияFTP530()
	Возврат 530;
КонецФункции

Функция КодСостоянияFTP550()
	Возврат 550;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли